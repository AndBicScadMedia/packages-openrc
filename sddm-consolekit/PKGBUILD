# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=sddm

pkgname=sddm-consolekit
pkgver=0.12.0
pkgrel=1
pkgdesc='QML based X11 display manager'
arch=('i686' 'x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'python-docutils' 'qt5-tools')
install="${_pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup')
conflicts=('display-manager' 'sddm')
provides=('display-manager' 'sddm')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz"
        "${_pkgname}-consolekit.patch"
        "${_pkgname}-respect-user-flags.patch")
sha256sums=('5027db5185d92ac5e5e547c7ba29693876ae3f0936d21fccbdd49af88aa1e89d'
            '4cbe522c84956d13d751856b8c66f2beca210c9ec65712e261bfe2bf94fb0da5'
            'a123cdc2e3337878b23e79b03ee54f859af5f8895dd2d9f80094780b3f5108bf')

prepare() {
  mkdir -p build
  cd ${_pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/${_pkgname}-consolekit.patch
  patch -p0 -i ${srcdir}/${_pkgname}-respect-user-flags.patch
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DDBUS_CONFIG_FILENAME=org.freedesktop.sddm.conf \
        -DBUILD_MAN_PAGES=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
