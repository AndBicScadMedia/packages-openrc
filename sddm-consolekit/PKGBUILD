# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=sddm

pkgname=sddm-consolekit
pkgver=0.11.0
pkgrel=2
pkgdesc='QML based X11 display manager'
arch=('i686' 'x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'python-docutils' 'qt5-tools')
install="${_pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup')
conflicts=('display-manager' 'sddm')
provides=('display-manager' 'sddm')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz"
        "${_pkgname}-consolekit.patch"
        "${_pkgname}-upower.patch"
        "${_pkgname}-dbus-config.patch")
sha256sums=('80d53f075c275f744bd547523d6d640baccfacb4e463a71ec0facb8231b832ce'
            '4cbe522c84956d13d751856b8c66f2beca210c9ec65712e261bfe2bf94fb0da5'
            '6a2efbc0cd65d8f63a3f1288051001b18da5d00f5ce0ddbac1656a69c3a1a29f'
            '6d4bf5df7c98f5193cd3574cd48c5b22930f51dbafd22b56015224bf38913412')

prepare() {
  mkdir -p build
  cd ${_pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/${_pkgname}-consolekit.patch
  patch -p1 -i ${srcdir}/${_pkgname}-upower.patch
  patch -p1 -i ${srcdir}/${_pkgname}-dbus-config.patch
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
        -DBUILD_MAN_PAGES=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
