# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=sddm

pkgname=sddm-consolekit
pkgver=0.13.0
pkgrel=1
pkgdesc='QML based X11 display manager'
arch=('i686' 'x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'python-docutils' 'qt5-tools')
install="${_pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup')
conflicts=('display-manager' 'sddm')
provides=('display-manager' 'sddm')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz"
        "${_pkgname}-consolekit.patch")
sha256sums=('26dee7e59859b5cbd5ec26b0050ef31c32b43be304e4042119819ab9779c26bd'
            '4cbe522c84956d13d751856b8c66f2beca210c9ec65712e261bfe2bf94fb0da5')

prepare() {
	mkdir -p build
	cd ${_pkgname}-${pkgver}
	patch -p1 -i ${srcdir}/${_pkgname}-consolekit.patch
}

build() {
	cd build
	cmake ../${_pkgname}-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
		-DDBUS_CONFIG_FILENAME=org.freedesktop.sddm.conf \
		-DBUILD_MAN_PAGES=ON
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
	sed -e 's|pam_systemd.so|pam_ck_connector.so nox11|' -i ${pkgdir}/etc/pam.d/sddm-greeter
}
