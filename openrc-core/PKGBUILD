# Maintainer: artoo <artoo@manjaro.org>
# Contributor: williamh <williamh@gentoo.org>

_src_uri="https://github.com"

_rc_uri="${_src_uri}/OpenRC/openrc/archive"
_net_uri="${_src_uri}/gentoo/netifrc/archive"
_func_uri="${_src_uri}/gentoo/gentoo-functions/archive"

_rc_ver=0.18.2
_net_ver=0.3.1
_func_ver=0.10

pkgver=${_rc_ver}
pkgrel=2
pkgbase=openrc-core
pkgname=('openrc' 'netifrc')
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
source=("openrc-${_rc_ver}.tar.gz::${_rc_uri}/${_rc_ver}.tar.gz"
	"netifrc-${_net_ver}.tar.gz::${_net_uri}/${_net_ver}.tar.gz"
	"gentoo-functions-${_func_ver}.tar.gz::${_func_uri}/${_func_ver}.tar.gz"
	"openrc.logrotate"
# 	'kmod-static-nodes'
	'LICENSE')
# 	'use-optional-modules-load-d.patch')
sha256sums=('89ef0e4c7edc38091ccc6bd70cacc6b306343ced0f52bfc060296566b5e2ce83'
            '8ae41223cdc735eee8303e93959b31b6aeae6f1a08661ef5baa6373dd86419c7'
            '709c8b22f404001a512e47a7a4d3192070b3e150fb9d0f943de09736d665b0db'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '28875cc6cb52e93657b96cf1bf0cc3b91891cdcbbc3196007c8cd93c6e22078f')

_configure_args() {
    _base_args=(SYSCONFDIR=/etc)
    if [ -f /etc/os-release ]; then
        . /etc/os-release
        _base_args+=(BRANDING="$NAME")
        _id=$ID
    else
        _base_args+=(BRANDING='Unknown Linux')
        _id=manjaro
    fi
    _base_args+=(PREFIX=/usr)
    _base_args+=(SBINDIR=/usr/bin)

    _rc_args=( "${_base_args[@]}" )
    _rc_args+=(LIBEXECDIR=/usr/lib/rc)
    _rc_args+=(MKSELINUX=no)
    _rc_args+=(MKPAM=pam)
    _rc_args+=(MKTERMCAP=ncurses)
    _rc_args+=(MKNET=no)

    _func_args+=(ROOTPREFIX=/usr)
    _func_args+=(ROOTSBINDIR=/usr/bin)
    _func_args+=(ROOTLIBEXECDIR=/usr/lib/$_id)

    _net_args=( "${_base_args[@]}" )
    _net_args+=(LIBEXECDIR=/usr/lib/netifrc)
}

_configure_args

prepare(){
	cd "${srcdir}/openrc-${pkgver}"
	sed -e "s|/sbin|/usr/bin|g" -i support/sysvinit/inittab
	#[[ $_id == 'manjaro' ]] && patch -Np1 -i ${srcdir}/use-optional-modules-load-d.patch
}

build(){
	# make openrc
	cd "${srcdir}/openrc-${pkgver}"
	make "${_rc_args[@]}"
	# make netifrc
	cd "${srcdir}/netifrc-${_net_ver}"
	make "${_net_args[@]}"
	cd ${srcdir}/gentoo-functions-${_func_ver}
	make "${_func_args[@]}"
}

package_openrc() {
        pkgver=${_rc_ver}
#         pkgrel=1
        pkgdesc="Gentoo's universal init system"
        depends=('inetutils' 'psmisc' 'sysvinit')
        optdepends=('netifrc: Network Interface Management Scripts'
                    'networkmanager-openrc: NetworkManager'
                    'connman-openrc: connman')
        conflicts=('systemd-sysvcompat' 'openrc-core')
        replaces=('openrc-core')
        install=${pkgname}.install
        backup=('etc/rc.conf'
                'etc/conf.d/consolefont'
                'etc/conf.d/keymaps'
                'etc/conf.d/hostname'
                'etc/conf.d/modules'
                'etc/conf.d/hwclock'
                'etc/inittab')

	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" "${_rc_args[@]}" install

	install -m644 "${srcdir}/${pkgname}-${pkgver}/support/sysvinit/inittab" "${pkgdir}/etc/inittab"
	install -Dm644 "${srcdir}/${pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgname}"

	sed -e 's/#unicode="NO"/unicode="YES"/' \
	    -e 's/#rc_logger="YES"/rc_logger="YES"/' \
	    -i "${pkgdir}/etc/rc.conf"

	install -d ${pkgdir}/usr/lib/rc/cache

	# kmod-static-nodes
# 	cd "${srcdir}/${pkgname}-${pkgver}"
# 	install -Dm755 "${srcdir}/kmod-static-nodes" "${pkgdir}/etc/init.d/kmod-static-nodes"
# 	ln -sf "/etc/init.d/kmod-static-nodes" "${pkgdir}/etc/runlevels/sysinit/kmod-static-nodes"

	install -Dm644 ${srcdir}/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

package_netifrc() {
        pkgver=${_net_ver}
#         pkgrel=2
        groups=('openrc-base')
        pkgdesc="Gentoo Network Interface Management Scripts"
        depends=('openrc' 'udev-openrc')
        install=${pkgname}.install
        backup=('etc/conf.d/net')

        cd "${srcdir}/${pkgname}-${pkgver}"
        make DESTDIR="${pkgdir}" "${_net_args[@]}" install
        install -Dm 644 "${srcdir}/${pkgname}-${pkgver}/doc/net.example" "${pkgdir}/etc/conf.d/net"

        install -d "${pkgdir}/etc/runlevels//boot"
        sed -e 's|#!/usr/bin/runscript|#!/usr/bin/openrc-run|' \
            -i "${pkgdir}/etc/init.d/net.lo"
        ln -sf "/etc/init.d/net.lo" "${pkgdir}/etc/runlevels/boot/net.lo"

        cd ${srcdir}/gentoo-functions-${_func_ver}
        make DESTDIR="${pkgdir}" "${_func_args[@]}" install

        sed -e "s|/lib/gentoo/functions.sh|/usr/lib/$_id/functions.sh|g" \
                -i "${pkgdir}/usr/lib/${pkgname}/sh/functions.sh"

        install -Dm644 ${srcdir}/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
