# Maintainer: artoo <artoo@manjaro.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com/OpenRC/openrc/archive"

_use_mld_patch=true

pkgname=openrc
pkgver=0.19
pkgrel=1
pkgdesc="Gentoo's universal init system"
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
depends=('inetutils' 'psmisc' 'sysvinit')
optdepends=('netifrc: Network Interface Management Scripts'
            'networkmanager-openrc: NetworkManager'
            'connman-openrc: connman')
conflicts=('systemd-sysvcompat' 'openrc-core')
replaces=('openrc-core')
install=${pkgname}.install
backup=('etc/rc.conf'
        'etc/conf.d/consolefont'
        'etc/conf.d/keymaps'
        'etc/conf.d/hostname'
        'etc/conf.d/modules'
        'etc/conf.d/hwclock'
        'etc/inittab')
source=("${pkgname}-${pkgver}.tar.gz::${_url}/${pkgver}.tar.gz"
	"${pkgname}.logrotate"
	'LICENSE')
sha256sums=('799c62034dc5fa66c5dea6c0f642df9b755cbfae3938fccbd48c00ddd2c786d0'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            '28875cc6cb52e93657b96cf1bf0cc3b91891cdcbbc3196007c8cd93c6e22078f')

${_use_mld_patch} && source+=('use-optional-modules-load-d.patch') \
		&& sha256sums+=('54e0cfb59b83ad42ec85486b99b7fed52ccbc8f98f0f113668eacf48a24a2d03')

if [ -f /etc/os-release ]; then
    . /etc/os-release
    _args=(BRANDING="$NAME")
else
    _args=(BRANDING='Unknown Linux')
fi
_args+=(SYSCONFDIR=/etc)
_args+=(PREFIX=/usr)
_args+=(SBINDIR=/usr/bin)
_args+=(LIBEXECDIR=/usr/lib/rc)
_args+=(MKSELINUX=no)
_args+=(MKPAM=pam)
_args+=(MKTERMCAP=ncurses)
_args+=(MKNET=no)


prepare(){
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -e "s|/sbin|/usr/bin|g" -i support/sysvinit/inittab
	${_use_mld_patch} && patch -Np1 -i ${srcdir}/use-optional-modules-load-d.patch
}

build(){
	cd "${srcdir}/${pkgname}-${pkgver}"
	make "${_args[@]}"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" "${_args[@]}" install

	install -m644 "${srcdir}/${pkgname}-${pkgver}/support/sysvinit/inittab" "${pkgdir}/etc/inittab"
	install -Dm644 "${srcdir}/${pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgname}"

	sed -e 's/#unicode="NO"/unicode="YES"/' \
	    -e 's/#rc_logger="YES"/rc_logger="YES"/' \
	    -i "${pkgdir}/etc/rc.conf"

	install -d ${pkgdir}/usr/lib/rc/cache

	install -Dm644 ${srcdir}/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
