# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Philip MÃ¼ller <philm|manjaro|org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

_spkgrel=1
_repo=http://mirror.netcologne.de/archlinux/core/os

pkgname=eudev-systemdcompat
pkgver=226
pkgrel=1
pkgdesc="systemd client libraries without udev; systemd compatibility package"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('eudev-base')
url="http://www.freedesktop.org/wiki/Software/systemd"
provides=("libsystemd=${pkgver}"
            "systemd=${pkgver}"
            "systemd-tools=${pkgver}"
            'libsystemd.so'
            'libsystemd-daemon.so'
            'libsystemd-id128.so'
            'libsystemd-journal.so'
            'libsystemd-login.so')
depends=('glib2' 'glibc' 'libgcrypt' 'xz' 'eudev>=3.1.2' 'lz4')
optdepends=('upower-pm-utils: pm-utils support')
conflicts=('systemd'
            'systemd-tools'
            'libsystemd'
            'openrc-systemdcompat')
replaces=('openrc-systemdcompat')

if [ "$CARCH" = "i686" ]; then
    source=("$_repo/i686/libsystemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz"
            "$_repo/i686/systemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz")
    sha256sums=('ee98131ccaea76f1087bef8528ad76eb299b92fbf6d84fb3718e83303f923fce'
                '91570b5b87af2b38505880f6dc979197edd4555539747678ac1d3dad4c880108')
elif [ "$CARCH" = "x86_64" ]; then
    source=("$_repo/x86_64/libsystemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
            "$_repo/x86_64/systemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
            "gummibootx64.efi")
    sha256sums=('5d2f6b19c5c566143a9bf4d1c72b150fff853291544435e4a649cfcf282711aa'
                '6ca54cb2320dac3173bff9234c5b108ed05b7d97c1a17b6a111a33133ffca789'
                'e0c6a40c74dc3a597dda977cb44bdf21759b8869e152d898d35664b8d3675fd3')
fi

prepare() {
	#clean libsystemd
	rm usr/lib/libudev*.so*

	# clean systemd
	rm -r {etc,var,usr/{include,share}}
	rm usr/lib/libnss*
	for d in usr/lib/*;do
            [[ -d $d ]] && rm -r $d
	done
	for f in usr/bin/*;do
		[[ $f != 'usr/bin/systemd-tmpfiles' && $f != 'usr/bin/systemd-sysusers' ]] && rm $f
	done
}

package() {
	mv "$srcdir/usr/" "$pkgdir"
	if [[ $CARCH == 'x86_64' ]];then
            install -D $srcdir/gummibootx64.efi $pkgdir/usr/lib/systemd/boot/efi/systemd-bootx64.efi
	fi
}
