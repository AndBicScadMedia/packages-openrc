# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Philip MÃ¼ller <philm|manjaro|org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

_spkgrel=3
_repo=http://mirror.netcologne.de/archlinux/core/os

pkgname=eudev-systemdcompat
pkgver=228
pkgrel=1
pkgdesc="systemd client libraries without udev; systemd compatibility package"
arch=('i686' 'x86_64')
license=('GPL2')
#groups=('eudev-base')
url="http://www.freedesktop.org/wiki/Software/systemd"
provides=("libsystemd=${pkgver}"
            "systemd=${pkgver}"
            "systemd-tools=${pkgver}"
            'libsystemd.so'
            'libsystemd-daemon.so'
            'libsystemd-id128.so'
            'libsystemd-journal.so'
            'libsystemd-login.so')
depends=('glib2' 'glibc' 'libgcrypt' 'xz' 'eudev' 'lz4')
conflicts=('systemd'
            'systemd-tools'
            'libsystemd'
            'openrc-systemdcompat')
replaces=('openrc-systemdcompat')

if [ "$CARCH" = "i686" ]; then
    source=("$_repo/i686/libsystemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz"
            "$_repo/i686/systemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz")
sha256sums=('6309a05c8eb250908918648d76c1c8635f5328d0e400ad8380499b9f203df313'
            '6780c6fb7c3c46af734f4ea3c9b7a25102f7003937413835d51592ff0ddd9504')
elif [ "$CARCH" = "x86_64" ]; then
    source=("$_repo/x86_64/libsystemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
            "$_repo/x86_64/systemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
            "gummibootx64.efi")
sha256sums=('71fe8955cbd5e32ab628af0f2a20dab0e93be5834934818120424dade0288b00'
            '35f377f39aaf9ff641d32ea4304ab2bb7fff2d6d70e275b5cd003c4707b5c660'
            'e0c6a40c74dc3a597dda977cb44bdf21759b8869e152d898d35664b8d3675fd3')
fi

prepare() {
	#clean libsystemd
	rm usr/lib/libudev*.so*

	# clean systemd
	rm -r {etc,var,usr/{include,share}}
	rm usr/lib/libnss*
	for d in usr/lib/*;do
            [[ -d $d ]] && rm -r $d
	done
	for f in usr/bin/*;do
		[[ $f != 'usr/bin/systemd-tmpfiles' && $f != 'usr/bin/systemd-sysusers' ]] && rm $f
	done
}

package() {
	mv "$srcdir/usr/" "$pkgdir"
	if [[ $CARCH == 'x86_64' ]];then
            install -D $srcdir/gummibootx64.efi $pkgdir/usr/lib/systemd/boot/efi/systemd-bootx64.efi
	fi
}
